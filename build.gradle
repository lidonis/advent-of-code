plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id 'io.gitlab.arturbosch.detekt' version '1.23.3'
    id 'com.palantir.graal' version '0.12.0'
    id 'me.champeau.jmh' version '0.7.2'
}

group 'fr.lidonis'
version '1.0-SNAPSHOT'

def jvmVersion = JavaVersion.VERSION_11.toString()

sourceCompatibility = jvmVersion
targetCompatibility = jvmVersion

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.kotlin.link")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3-native-mt"
    implementation "info.picocli:picocli:4.7.5"
    implementation "org.fusesource.jansi:jansi:2.4.1"

    implementation "com.fasterxml.jackson.core:jackson-databind:2.16.0"
    implementation "space.kscience:kmath-polynomial:0.3.1-dev-5"

    annotationProcessor "info.picocli:picocli-codegen:4.7.5"
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.9.21"
    testImplementation "org.assertj:assertj-core:3.24.2"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.1"
    testImplementation "io.mockk:mockk:1.13.8"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

test {
    useJUnitPlatform()
}

application {
    mainClass.set('fr.lidonis.adventofcode.AdventOfCodeMain')
}

graal {
    mainClass application.getMainClass().get()
    outputName rootProject.name
    javaVersion jvmVersion
    option '--static'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = jvmVersion
        freeCompilerArgs = ["-opt-in=kotlin.RequiresOptIn", "-Xcontext-receivers"]
        languageVersion = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = jvmVersion
        languageVersion = "1.8"
    }
}